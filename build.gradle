apply plugin: 'war'
apply plugin: 'groovy'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
    
    maven{
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    providedCompile 'javax:javaee-api:7.0',
                    'org.projectlombok:lombok:1.16.6'

    compile 'org.omnifaces:omnifaces:2.4-SNAPSHOT'

    testCompile 'junit:junit:4.10',
                'org.spockframework:spock-core:1.0-groovy-2.4'
}


if (project.hasProperty('evaluatingIDE') && project.property('evaluatingIDE') == 'NetBeans') {
    sourceSets.main.java.srcDir(webAppDir.toString() )
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}


ext.deployBase="/opt/wildfly/standalone/deployments"
ext.deployTarget="$deployBase/$war.archiveName"
ext.dodeployLCFname = "$deployTarget" + ".dodeploy"
ext.deployedLCFname="$deployTarget" + ".deployed"
ext.undeployedLCFname="$deployTarget" + ".undeployed"

task updateResources(type: Copy) {
    from 'src/main/webapp'
    into deployTarget
    include '**/*.xhtml'
}

task deployWar(type: Copy){

    into deployTarget
    with war

    doLast{
        def f = file(dodeployLCFname)
        f.lastModified = System.currentTimeMillis()
        f.createNewFile()
    }
}

task undeployWar <<{
    delete deployedLCFname
    delete deployTarget
    delete undeployedLCFname
}

deployWar.dependsOn war